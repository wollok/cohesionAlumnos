import alumnos.*

describe "tests sobre alumnos" {
	
	test "un alumno estudioso estudia siempre" {
		const alumnoEstudioso = new Alumno()
		const parcial = new Parcial(cantidadPreguntas = 5)

		assert.that(alumnoEstudioso.estudiaPara(parcial))
	}

	test "un alumno vago no estudia si le fue bien" {
		const vagoQueLeFueBien = new Alumno(tipoAlumno = vago)
		vagoQueLeFueBien.rendir(9)
		const parcial = new Parcial(cantidadPreguntas = 5) 

		assert.notThat(vagoQueLeFueBien.estudiaPara(parcial))
	}

	test "un alumno vago estudia si le fue mal" {
		const vagoQueLeFueMal = new Alumno(tipoAlumno = vago)
		vagoQueLeFueMal.rendir(2)
		const parcial = new Parcial(cantidadPreguntas = 5)

		assert.that(vagoQueLeFueMal.estudiaPara(parcial))
	}

	test "un alumno hijo del rigor no estudia para un parcial facil" {
		const alumnoHijoDelRigor = new Alumno(tipoAlumno = hijoDelRigor)
		const parcialFacil = new Parcial(cantidadPreguntas = 3) 

		assert.notThat(alumnoHijoDelRigor.estudiaPara(parcialFacil))
	}

	test "un alumno hijo del rigor estudia para un parcial dificil" {
		const alumnoHijoDelRigor = new Alumno(tipoAlumno = hijoDelRigor)
		const parcialDificil = new Parcial(cantidadPreguntas = 8)

		assert.that(alumnoHijoDelRigor.estudiaPara(parcialDificil))
	}

}
